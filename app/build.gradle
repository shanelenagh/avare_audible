apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
android {
    compileSdkVersion 32
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "com.ds.avare"
        minSdkVersion 20 // android 5 is minimum
        targetSdkVersion 32
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencies {
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false // for Travis CI
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests {
                includeAndroidResources = true
        }
    }
    useLibrary 'android.test.mock'

    applicationVariants.all { variant ->
        def productFlavor = variant.productFlavors[0] != null ? "${variant.productFlavors[0].name.capitalize()}" : ""
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["compile${productFlavor}${buildType}UnitTestSources"].dependsOn(tasks["merge${productFlavor}${buildType}Assets"])
    }
}

configurations {
    jmh
}

task runAdibleAlertsConsumerBenchmarks(type: JavaExec, dependsOn: ['compileDebugJavaWithJavac', 'compileDebugUnitTestJavaWithJavac']) {
    group = "Execution"
    description = "Run the main class with JavaExecTask"
    classpath = configurations.jmh
    classpath += files("build/intermediates/javac/debugUnitTest/classes", "build/intermediates/javac/debug/classes", "${android.getSdkDirectory().getAbsolutePath()}\\platforms\\${android.compileSdkVersion}\\android.jar")
    main = 'com.ds.avare.adsb.AudibleTrafficAlertsConsumerBenchmarks'
}

dependencies {
    implementation 'org.xmlunit:xmlunit-core:2.3.0'
    implementation 'oro:oro:2.0.8'
    implementation 'org.xmlunit:xmlunit-matchers:2.3.0'
    implementation 'androidx.core:core:1.3.2'
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.6'
    implementation 'com.github.mik3y:usb-serial-for-android:3.4.6'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-inline:4.8.1'
    testImplementation 'org.openjdk.jmh:jmh-core:1.36'
    jmh 'org.openjdk.jmh:jmh-core:1.36'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
    jmh 'org.mockito:mockito-inline:4.8.1'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
    testImplementation 'org.json:json:20220924'
}
